### 1.3高阶过程

#### exercise1.29

整个代码如下所示：

```scheme
(define (Sipson-integral f a b n)
 (define h (/ (- b a) n))

 (define (cof k)
  (cond ((or (= k 0) (= k n)) 1)
         ((even? k) 2)
         (else 4)))

 (define (term n)
  (* (cof n)
     (f (+ a (* n h)))))
 
 (define (inc n) (+ n 1))
 
 (* (sum term 0 inc n)
    (/ h 3.0)))
```

上述代码的执行情况如下：

```bash
1 ]=> (Sipson-integral cube 0 1 100)
;Value: .25

1 ]=> (Sipson-integral cube 0 1 1000)
;Value: .25
```

能直接得到0.25的正确值而没有任何偏差是我所没有预料到的。

#### exercise1.30

从书中给出的提示不难看出。在``iter``过程中，使用一个参数``a``来表示当前的下标，使用一个结果``result``来表示当前已经完成计算的部分和。完整的代码如下所示：

```scheme
(define (sum term a next b)
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (+ (term a) result))))
  (iter a 0))
```

将该过程用于``integral``的计算并与之前的计算结果对比就能发现结果是正确的。

#### exercise1.31

##### (a)

定义``product``的方法和定义``sum``的方法类似，只是将``sum``中的加操作替换为乘法操作。代码如下：

```scheme
(define (product term a next b)
    (if (> a b)
        1
        (* (term a) (product term (next a) next b))))

(define (factorial n)
    (define (inc n) (+ n 1))
    (define (id n) n)
    (product id 1 inc n))
```

而``factorial``， 意为阶乘，实际上也只是从1到n的范围内求所有数的乘积。

计算``pi``的时候，我们需要分别对分子和分母计算部分和然后再相除。计算分子时，通过观察我们可以发现，偶数下标的值就是将下标加2，奇数下标就是加一。对于分母，则恰好相反。代码如下所示：

```scheme
(define (pi-sum n)

    (define (inc n) (+ n 1))

    (define (numer n)
        (if (even? n)
            (+ n 2)
            (+ n 1)))

    (define (denom n)
        (if (even? n)
            (+ n 1)
            (+ n 2)))
    (* 4.0
       (/ (product numer 1 inc n)
          (product denom 1 inc n))))
```

最后将结果乘以4.0直接得到``pi``的值。计算的结果如下：

```bash
1 ]=> (pi-sum 100)
;Value: 3.1570301764551676

1 ]=> (pi-sum 1000)
;Value: 3.1431607055322663

1 ]=> (pi-sum 10000)
;Value: 3.1417497057380523
```

随着n的增加，计算结果越来越精确，但速度也越来越慢。

##### (b)

将上述递归计算过程改为迭代计算过程的方法和``1-30``中的方法一样，每次都使用一个参数作为计算得到的部分和进行传递。代码如下：

```scheme
(define (product-iter term a next b)
    (define (iter a result)
        (if (> a b)
            result
            (iter (next a) (* (term a) result))))
    (iter a 1))
```

#### exercise1.32

```scheme
(define (accumulate combiner null-value term a next b)
    (if (> a b)
        null-value
        (combiner (term a)
                  (accumulate combiner null-value term (next a) next b))))

(define (sum term a next b)
    (define (add a b) (+ a b))
    (accumulate add 0 term a next b))

(define (product term a next b)
    (define (mul a b) (* a b))
    (accumulate mul 1 term a next b))
```

改写为迭代的做法与前面几题的做法相同。

```scheme
(define (accumulate-iter combiner null-value term a next b)
    (define (iter a result)
        (if (> a b)
            result
            (iter (next a) (combiner (term a) result))))
    (iter 1 null-value))
```

#### exercise1.33

本题需要我们实现有条件地combine两个结果。所以通过参数中给出的谓词``filtered``实现另一个局部的``filtered-combiner``，其逻辑是当前新加入的数如果满足条件就调用``combiner``将其组合在一起，否则就返回原先的数。然后调用``filtered-combiner``用于该过程：

```scheme
(define (filtered-accumulate combiner null-value filter? term a next b)
    (define (filtered-combiner a b)
        (if (filter? a)
            (combiner a b)
            b))
    (if (> a b)
        null-value
        (filtered-combiner (term a)
                           (filtered-accumulate combiner null-value filter? term (next a) next b))))
```

在第一部分求素数之和时，如果采用书上33页的程序检测素数会导致将1也判断为素数，这是不符合数学定义的，所以在``prime?``谓词中要单独加入对1的判断逻辑。如下：

```scheme
(define (prime? n)
    (if( = n 1)
       false
       (= (smallest-divisor n) n)))
```

其余的完整代码可参考``code``文件夹下的``33-filtered-accumulate.scm``文件。

#### exercise1.34

在``mit-scheme``中运行的结果如下：

```
;The object 2 is not applicable
```

产生这个现象的原因。当我们执行``(f f)``时，根据定义，会展开为``(f 2)``, 改表达式又会进一步展开为``(2 2)``, 2 不能作为一个过程调用，所以产生了错误。

#### exercise1.35

$x\rightarrow 1+1/x$ 的不动点满足
$$
x=1+1/x
$$
即:$x^2-x-1=0$, 这正是黄金分割的定义。

将``fixed-point``方法应用于过程``(lambda (x) (+ 1 (/ 1 x)))``上就能得到黄金分割的值。结果为1.6180327868852458。完整代码参考``code``文件夹下的``35-fixpoint-phi.scm``。

#### exercise1.36

将``fixed-point``分别用于以下两个过程：

```scheme
(define (root-without-average) (fixed-point (lambda (x) (/ (log 1000) (log x))) 5.0))
(define (root-with-average) (fixed-point (lambda (x) (average x (/ (log 1000) (log x)))) 5.0))
```

最终的运算结果如下：

```bash
1 ]=> (root-without-average)
5
4.29202967422018
...
4.555530232469306
;Value: 4.555530232469306

1 ]=> (root-with-average)
5
4.64601483711009
...
4.55553802710999
;Value: 4.5555361005218895
```

完整的中间计算过程没有写出来。但是结果是采用了平均阻尼的计算方法的运算次数小于没有采用平均阻尼的方法。

#### exercise1.37

##### (a)

针对此题的思路是，根据连分式的形式求出通项公式。对于求k项的有限连分式。我们可以从最底部的k开始求，为此，定义了以下的序列$f_i$：
$$
f_i=\cfrac{N_i}{D_i+\cfrac{N_{i+1}}{...+\cfrac{N_k}{D_k}}}
$$
从该定义不难看出，有以下递推公式：
$$
f_{i-1}=\cfrac{N_{i-1}}{D_{i-1}+f_i}
$$
因此我们可以定义一个辅助过程，依次计算$f_i$, 而要求计算的 $k$ 项连分式就是$f_1$. 代码如下：

```scheme
(define (cont-frac n d k)
    (define (f i)
        (if (= i k)
            (/ (d i) (n i))
            (/ (n i) (+ (d i) (f (+ i 1))))))
    (f 1))

(define (get-phi k)
    (cont-frac (lambda (i) 1.0)
               (lambda (i) 1.0)
               k))
```

计算结果如下：

```bash
1 ]=> (get-phi 10)
;Value: .6179775280898876

1 ]=> (get-phi 11)
;Value: .6180555555555556
```

由此可以看出，需要计算到11位才能将精度控制在小数点后4位处。

##### （b)

将上述过程修改为迭代计算过程，只需将``f``修改为迭代过程即可。

```scheme
(define (cont-frac-iter n d k)
    (define (f-iter i result)
        (if (= i 1)
            result
            (f-iter (- i 1) (/ (n (- i 1)) (+ (d (- i 1)) result)))))
    (f-iter k (/ (n k) (d k))))

(define (get-phi-iter k)
    (cont-frac-iter (lambda (i) 1.0)
                    (lambda (i) 1.0)
                    k))
```

测试结果和上面使用递归计算过程的测试结果相同

##### exercise1.38

使用上一题得到的连分式计算方法，可以得到计算e的方法如下：

```scheme
(define (d i)
    (cond ((or (= (remainder i 3) 0) (= (remainder i 3) 1)) 1)
           (else (* (/ (+ i 1) 3) 2))))

(define (get-e k)
    (+ 2 (cont-frac (lambda (i) 1.0)
               d
               k)))
```

计算结果如下：

```bash
1 ]=> (get-e 100)
;Value: 2.7182818284590455
```

#### exercise1.39

本题只需写出对应的N和D的表达式，然后再调用``cont-frac``计算即可。这里要注意的是``N``从2开始就是``-x^2``了，而不是正的。代码如下：

```scheme
(define (tan-cf x k)
    (define (N i)
        (cond ((= i 1) x)
            (else (- 0 (* x x)))))
    (define (D i)
        (- (* 2 i) 1))
    
    (exact->inexact (cont-frac N D k)))
```

测试结果如下：

```bash
(tan-cf 20 1000)
;Value: 2.237160944224742
```

该结果与使用``scheme``内置的``tan``函数得到的结果相同。

#### exercise1.40

题目要求我们根据系数``a,b,c``返回一个过程，计算``x^3 +ax^2 + bx +c``.该过程定义如下：

```scheme
(define (cubic a b c)
    (lambda (x) (+  (* x x x)
                    (* a (* x x))
                    (* b x)
                    c)))
```

通过测试一系列数据，结果如下：

```bash
1 ]=> (newtons-method (cubic 3 2 1) 1.0)
;Value: -2.3247179572447267

1 ]=> (newtons-method (cubic 2 5 5) 1.0)
;Value: -1.2332293376819243
```

#### exercise1.41

代码如下：

```scheme
(define (double f)
    (lambda (x) (f (f x))))
```

对于书中表达式运行结果如下：

```bash
1 ]=> (((double (double double)) inc) 5)
;Value: 21
```

#### exercise1.42

代码如下

```scheme
(define (compose f g) 
    (lambda (x) (f (g x))))

(define (inc n) (+ n 1))
(define (square x) (* x x))
```

#### exercise1.43

使用题目1.42中的compose过程以及递归定义可以很简单地完成代码:

```scheme
(define (repeated f n)
    (if (= n 1)
        f
        (compose f (repeated f (- n 1)))))
```

#### exercise1.44

smooth的代码如下：

```scheme
(define (smooth f)
    (define dx 0.00001)
    (lambda (x) (/  (+  (f (- x dx))
                        (f x)
                        (f (+ x dx)))
                    3)))
```

n次平滑的代码如下：

```scheme
(define (repeated-smooth f n)
    ((repeated smooth n) f))
```

