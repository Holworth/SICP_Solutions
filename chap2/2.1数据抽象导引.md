#### exercise2.1

只需要判断一下分子分母乘积的正负性即可。如果``n * d``大于0，说明分子分母同正负，此时只需要将参数n，d取绝对值组成序对即可；否则说明分子分母奇偶性不同，此时需要将分母设置为绝对值，分子设置为绝对值加一个负号。代码如下所示：

```scheme
(define (gcd a b)
    (if (= b 0)
        a
        (gcd b (remainder a b))))

(define (make-rat-help n d)
    (let ((g (gcd (abs n) (abs d))))
         (cons (/ n g) (/ d g))))

(define (make-rat n d)
    (cond   ((< (* n d) 0) (make-rat-help (-(abs n)) (abs d)))
            (else (make-rat-help (abs n) (abs d)))))

(define (numer x) (car x))
(define (denom x) (cdr x))

(define (print-rat x)
    (newline)
    (display (numer x))
    (display "/")
    (display (denom x)))
```

这里值得注意的一点是gcd的处理，这里处理时统一对分子分母绝对值求的最大公因子。否则，当对一个正数，一个负数求最大公因子时，得到的结果是负数。在组成序对除以最大公因子时这会使得我们的结果与预想的结果相反。

测试结果如下：

```bash
1 ]=>(define a (make-rat 6 -4))
;Value: a
1 ]=>(print-rat a)
-3/2
;Unspcified return value

1 ]=>(define a (make-rat -4 -8))
;Value: a
1 ]=>(print-rat a)
1/2
;Unspcified return value

1 ]=>(define a (make-rat -6 4))
;Value: a
1 ]=>(print-rat a)
-3/2
;Unspcified return value

1 ]=>(define a (make-rat 6 4))
;Value: a
1 ]=>(print-rat a)
3/2
;Unspcified return value
```

#### exercise2.2

构造方法和书中提到的有理数系统的构造方法类似，先使用序对表示点，再使用点的序对表示一条线段。计算中点时，只需要将起始点的x,y 坐标分别求平均值即可。事实上，这样表示出来的线段是一条有向线段。

```scheme
(define (make-point x y)
    (cons x y))

(define (x-point point)
    (car point))

(define (y-point point)
    (cdr point))

(define (print-point point)
    (newline)
    (display "(")
    (display (x-point point))
    (display ",")
    (display (y-point point))
    (display ")"))

(define (make-seg start-point end-point)
    (cons start-point end-point)) 

(define (start-point seg)
    (car seg))

(define (end-point seg)
    (cdr seg))

(define (mid-point seg)
    (let ((start (start-point seg))
          (end   (end-point seg)))
    (make-point (average (x-point start) (x-point end))
                (average (y-point start) (y-point end)))))

(define (average a b) (/ (+ a b) 2))
```

#### exercise2.3

表示方法使用左上顶点和右下顶点的方法计算，周长就是两个顶点的x坐标差值与y坐标差值之和的2倍，面积是两个差值的乘积。代码如下：

```scheme
(define (make-point x y)
    (cons x y))

(define (x-point point)
    (car point))

(define (y-point point)
    (cdr point))

(define (make-rectangle left-above right-below)
    (cons left-above right-below))

(define (left-above rectangle)
    (car rectangle))

(define (right-below rectangle)
    (cdr rectangle))

(define (circum rectangle)
    (let ((labove (left-above rectangle))
          (rbelow (right-below rectangle)))
        (* (+ (- (x-point rbelow) (x-point labove))
              (- (y-point rbelow) (y-point labove)))
           2)))

(define (area rectangle)
    (let ((labove (left-above rectangle))
          (rbelow (right-below rectangle)))
         (* (- (x-point rbelow) (x-point labove))
            (- (y-point rbelow) (y-point labove)))
    ))
```

